plugins {
    id 'java'
    id 'signing'
    id 'java-library'
    id 'maven-publish'
}

group = 'com.yifeistudio'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://s01.oss.sonatype.org/content/groups/public/'
    }
}

java {
    withSourcesJar()
}

ext {
    lombokVersion = '1.18.24'
    springBootVersion = '2.6.7'
    spaceVersion = '0.0.1-SNAPSHOT'
    mybatisVersion = '3.5.9'
    mysqlVersion = '8.0.29'
    rocketMqVersion = '2.2.2'
}

dependencies {

    // 默认数据存储器
    compileOnly "org.mybatis:mybatis:${mybatisVersion}"

    // 默认消息通道
    compileOnly "org.apache.rocketmq:rocketmq-spring-boot-starter:${rocketMqVersion}"

    compileOnly "org.springframework.boot:spring-boot:${springBootVersion}"
    compileOnly "org.springframework.boot:spring-boot-autoconfigure:${springBootVersion}"
    compileOnly "org.springframework.boot:spring-boot-starter-aop:${springBootVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    api "com.yifeistudio:space-starter:${spaceVersion}"

    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}"
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign(publishing.publications["mavenJava"])
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('compileJava') {
    inputs.files(tasks.named('processResources'))
}
